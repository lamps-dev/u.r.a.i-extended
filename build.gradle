plugins {
    id 'fabric-loom' version '1.14.10'
    id 'maven-publish'
    id 'com.modrinth.minotaur' version '2.+'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}



repositories {
    maven { url = "https://maven.terraformersmc.com/releases/" }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
    maven { url = "https://maven.nucleoid.xyz/" }
    maven { url "https://maven.shedaniel.me"}
    maven { url "https://jitpack.io" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "com.terraformersmc:modmenu:17.0.0-beta.2"
    modImplementation "maven.modrinth:cloth-config:21.11.153+fabric"

    // U.R.A.I is required at runtime - users must install it separately
    // (We compile against it here for development)
    modImplementation files("libs/urai-1.0.1.jar")

    modImplementation "eu.pb4:placeholder-api:2.8.1+1.21.10"
    modApi("me.shedaniel.cloth:basic-math:+")

}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

// Modrinth publishing
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = project.modrinth_id
    versionNumber = project.mod_version
    versionType = "release"
    uploadFile = remapJar
    gameVersions = [project.minecraft_version]
    loaders = ["fabric"]
    changelog = System.getenv("CHANGELOG") ?: "See the full changelog at https://github.com/lamps-dev/u.r.a.i-extended/releases"
    dependencies {
        required.project "fabric-api"
        required.project "cloth-config"
        required.project "urai"
    }
}

// CurseForge publishing
curseforge {
    if (System.getenv("CURSEFORGE_TOKEN")) {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
    }
    project {
        id = project.curseforge_id
        changelog = "See the full changelog at https://github.com/yourusername/yourrepo/releases"
        releaseType = "release"
        addGameVersion project.minecraft_version
        addGameVersion "Fabric"
        addGameVersion "Java 21"

        mainArtifact(remapJar) {
            displayName = "${project.name} ${project.mod_version}"
        }

        relations {
            requiredDependency "fabric-api"
            requiredDependency "cloth-config"
            // U.R.A.I is bundled with this mod (Jar-in-Jar), no separate download needed
        }
    }
    options {
        forgeGradleIntegration = false
    }
}
